plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.rik'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

sourceSets {
	intTest {
		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
	}
}

configurations {
	intTestImplementation.extendsFrom implementation
	intTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.2.0'
	implementation 'org.springframework.boot:spring-boot-starter-web:3.2.0'
	implementation 'org.springframework.boot:spring-boot-starter-validation:3.2.0'
	implementation 'org.liquibase:liquibase-core:4.25.1'

	compileOnly 'org.projectlombok:lombok:1.18.30'

	runtimeOnly 'org.postgresql:postgresql:42.7.1'

	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	testImplementation 'org.springframework.boot:spring-boot-starter-test:3.2.0'

	intTestImplementation 'org.testcontainers:postgresql:1.19.3'
	intTestImplementation 'org.testcontainers:junit-jupiter:1.19.3'
	intTestImplementation 'org.springframework.boot:spring-boot-starter-test:3.2.0'
}

tasks.register('integrationTest', Test) {
	description = "Runs integration tests."
	group = "verification"

	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath = sourceSets.intTest.runtimeClasspath
}

tasks.test {
	useJUnitPlatform()
}

tasks.integrationTest {
	useJUnitPlatform()
	systemProperty("spring.profiles.active", "integration")
}

tasks.named('check') {
	dependsOn tasks.named('integrationTest')
}